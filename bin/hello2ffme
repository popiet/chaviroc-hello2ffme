#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;

use FindBin '$RealBin';
use lib "$RealBin/../local/lib/perl5";
use Text::CSV_XS;

use lib "$RealBin/../lib";
use Chaviroc::Hello2Ffme qw< data value conf conf_pays >;

# configuration file
# drives transformations
my $conf = "$RealBin/../etc/hello2ffme.csv";

# liste code pays
my $pays = "$RealBin/../etc/pays.csv";

GetOptions(
    "conf=s" => \$conf,
    "pays=s" => \$pays,
);

# fichier d'entrée : 1er argument ou stdin
my ($filein, $fileout) = @ARGV;
my $in = *STDIN;
if ($filein) {
    open $in, '<', $filein
        or die "pb open < $filein: $!";
}

# fichier sortie : 2e argument ou stdout
my $out = *STDOUT;
if ($fileout) {
    open $out, '>', $fileout
        or die "pb open > $fileout: $!";
}

# parser fichier conf
my @fields = conf($conf);

# parser fichier code pays
conf_pays($pays);

# read and write csv
# separator ';'
my $parser = Text::CSV_XS->new({
    sep_char => ';',
    allow_loose_quotes => 1,
    binary => 1,
    auto_diag => 1,
    eol => $/,
});

# first row helloasso (in)
my $head = $parser->getline($in);

# first row ffme (out)
$parser->print($out, [ map { $_->[0] } @fields ]);

while (my $row = $parser->getline($in)) {
    # key => value of the row
    my $data = data($row, $head);

    # new line to be written
    my @new;

    for my $field (@fields) {
        my $value = value($data, $field);
        push @new, $value;
    }

    $parser->print($out, \@new);
}

__END__

=encoding utf8

=head1 NAME

hello2ffme - Transformer le csv HelloAsso en un csv pour la FFME

=head1 INSTALLATION

    carton install

pour installer les modules perl nécéssaires

=head1 SYNOPSIS

    bin/hello2ffme [--conf etc/hello2ffme.csv] [--pays etc/pays.csv] < hello.csv > ffme.csv

=head1 DESCRIPTION

Le fichier d'entrée hello.csv est un csv encodé en UTF-8
dont le séparateur est le ';'

Le fichier de sortie ffme.csv sera un csv encodé en UTF-8
avec pour séparateur ';' et pour première ligne le nom des colonnes du fichier.

La transformation est pilotée par le fichier de configuration 
(par défaut etc/hello2ffme.csv)

C'est un csv avec comme séparateur le ';' et pour première ligne le nom des colonnes.

Il sert à faire correspondre un ou plusieurs champs du fichier d'entrée
vers un champ du fichier de sortie.

La première colonne "champ ffme" donne le nom du champ du fichier de sortie.
Les colonnes suivantes les valeurs du fichier d'entrée à faire correspondre.
Si plusieurs colonnes sont spécifiées, les valeurs correspondantes seront concaténées avec un espace.

Parfois la simple correspondance entre 2 champs des 2 fichiers est insuffisante.
Dans ce cas il est possible de spécifier dans le premier champ helloasso une fonction
en préfixant son nom par "fct:".
Les valeurs correspondant aux colonnes spécifiées suivantes seront données en argument à la fonction.

Par exemple, une ligne du fichier de conf

    ACTION;fct:action;licence

appelera la fonction Chaviroc::Hello2Ffme::action avec pour argument la valeur du champ licence du fichier d'entrée.
La valeur retournée valorisera la colonne ACTION du fichier de sortie.
